apply plugin: 'com.android.library'
apply plugin: 'realm-android'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

group = artifact_group
version = repoli_version_name

android {
    compileSdkVersion android_compilesdk_version
    buildToolsVersion android_buildtools_version

    defaultConfig {
        minSdkVersion android_minsdk_version
        targetSdkVersion android_targetsdk_version
        versionCode android_versioncode
        versionName repoli_version_name

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "org.jetbrains:annotations:${jetbrains_annotations_version}"
    compile "com.android.support:support-annotations:${android_support_version}"

    androidTestCompile("com.android.support.test.espresso:espresso-core:${espresso_version}", {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    testCompile "junit:junit:${junit_version}"

    compile "jp.s64.java.repoli:rxjava1:${repoli_version_name}"
}

bintray {
    user = bintray_user
    key = bintray_api_key

    configurations = default_configurations

    pkg {
        repo = default_repo
        name = 'repoli-realm'
        licenses = default_licenses
        websiteUrl = site_url
        issueTrackerUrl = issues_url
        vcsUrl = git_url
        labels = shared_labels
        publicDownloadNumbers = true

        version {
            name = repoli_version_name
            released = toIsoDateFormat(new Date())
            vcsTag = repoli_version_name
        }
    }

}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                scm {
                    url site_url
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar, javadocJar
}